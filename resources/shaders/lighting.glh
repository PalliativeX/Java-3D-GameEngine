uniform vec3 eyePos;
uniform float specularIntensity;
uniform float specularPower;

struct BaseLight
{
    vec4 color;
    float intensity;
};

// how quick the light weakens with distance
struct Attenuation
{
    float constant;
    float linear;
    float exponent;
};

struct PointLight
{
    BaseLight base;
    Attenuation attenuation;
    vec3 position;
    float range;
};

struct SpotLight
{
    PointLight pointLight;
    vec3 direction;
    float cutOff;
};

struct DirectionalLight
{
    BaseLight base;
    vec3 direction;
};

vec4 calcLight(BaseLight base, vec3 direction, vec3 normal, vec3 fragPos)
{
    float diffuseFactor = dot(normal, -direction);

    vec4 diffuseColor  = vec4(0, 0, 0, 0);
    vec4 specularColor = vec4(0, 0, 0, 0);

    if (diffuseFactor > 0) {
        diffuseColor = base.color * base.intensity * diffuseFactor;

        // calculating specular color
        vec3 directionToEye = normalize(eyePos - fragPos);
        //vec3 reflectDir = normalize(reflect(direction, normal));
        vec3 halfDirection = normalize(directionToEye - direction);

        float specularFactor = dot(halfDirection, normal);
        //float specularFactor = dot(directionToEye, reflectDir);
        specularFactor = pow(specularFactor, specularPower);

        if (specularFactor > 0) {
            specularColor = base.color * specularIntensity * specularFactor;
        }
    }

    return diffuseColor + specularColor;
}

vec4 calcPointLight(PointLight pointLight, vec3 normal, vec3 fragPos)
{
    vec3 lightDirection = fragPos - pointLight.position;
    float distanceToPoint = length(lightDirection);

    if (distanceToPoint > pointLight.range)
    return vec4(0, 0, 0, 0);

    lightDirection = normalize(lightDirection);

    vec4 color = calcLight(pointLight.base, lightDirection, normal, fragPos);

    float attenuation = pointLight.attenuation.constant +
    pointLight.attenuation.linear * distanceToPoint +
    pointLight.attenuation.exponent * distanceToPoint * distanceToPoint +
    0.0001;

    return color / attenuation;
}

vec4 calcSpotLight(SpotLight spotLight, vec3 normal, vec3 fragPos)
{
    vec3 lightDir = normalize(fragPos - spotLight.pointLight.position);
    float spotFactor = dot(lightDir, spotLight.direction);

    vec4 color = vec4(0, 0, 0, 0);

    if (spotFactor > spotLight.cutOff) {
        color = calcPointLight(spotLight.pointLight, normal, fragPos) *
        (1.0 - (1.0 - spotFactor)/(1.0 - spotLight.cutOff));
    }

    return color;
}


vec4 calcDirectionalLight(DirectionalLight directionalLight, vec3 normal, vec3 fragPos)
{
    return calcLight(directionalLight.base, -directionalLight.direction, normal, fragPos);
}